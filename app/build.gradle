plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-parcelize"
    id 'com.google.dagger.hilt.android'
    id 'com.google.devtools.ksp'
}

ext {
    publicRelease = project.hasProperty("public")
    isRelease = project.hasProperty("release")
}

@SuppressWarnings("GrMethodMayBeStatic")
def getVersionSuffix() {
    if (publicRelease) {
        return "-playstore"
    } else {
        return "-internal"
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 34

    defaultConfig {
        applicationId "io.github.plastix.buzz"
        if(!isRelease) {
            applicationIdSuffix ".dev"
        }
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 17
        versionName "1.0-alpha08"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "boolean", "IS_PUBLIC_RELEASE", publicRelease.toString()
        versionNameSuffix getVersionSuffix()
    }

    buildFeatures {
        compose true
        buildConfig true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.5.11"
    }
    namespace 'io.github.plastix.buzz'

}

hilt {
    enableAggregatingTask = true
}

ksp {
    arg('room.schemaLocation', "$projectDir/schemas")
}

dependencies {
    implementation(project(":core"))

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"

    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.compose.ui:ui:1.6.4"
    implementation "androidx.compose.ui:ui-tooling:1.6.4"
    implementation "androidx.compose.foundation:foundation:1.6.4"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0-alpha13"
    implementation "androidx.compose.material:material:1.6.4"
    implementation "androidx.compose.material:material-icons-core:1.6.4"
    implementation "androidx.compose.material:material-icons-extended:1.6.4"
    implementation "androidx.activity:activity-compose:1.8.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0"
    implementation "androidx.compose.runtime:runtime-livedata:1.6.4"
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.work:work-runtime-ktx:2.9.0"
    implementation "androidx.hilt:hilt-work:1.2.0"
    ksp "androidx.hilt:hilt-compiler:1.2.0"

    implementation "com.google.android.material:material:1.11.0"
    implementation "com.google.dagger:hilt-android:2.51.1"
    ksp "com.google.dagger:hilt-compiler:2.51.1"

    implementation("com.squareup.okhttp3:okhttp:4.9.3")
    implementation("com.squareup.moshi:moshi:1.13.0")
    ksp "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
    
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
}
