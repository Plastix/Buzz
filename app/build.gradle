plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

ext {
    publicRelease = project.hasProperty("public")
}

@SuppressWarnings('GrMethodMayBeStatic')
def getVersionSuffix() {
    if (publicRelease) {
        return "-playstore"
    } else {
        return "-internal"
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.github.plastix.buzz"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 4
        versionName "1.0-alpha02"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "boolean", "IS_PUBLIC_RELEASE", publicRelease.toString()
        versionNameSuffix getVersionSuffix()
    }

    buildFeatures {
        compose true
        buildConfig true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.0.0-beta04'
    }

    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation(project(":core"))

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.3.0-rc01'
    implementation("com.squareup.okhttp3:okhttp:4.9.1")
    implementation("com.squareup.moshi:moshi:1.11.0")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.11.0")
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.compose.ui:ui:1.0.0-beta05'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.0.0-beta05'
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.0.0-beta05'
    // Material Design
    implementation 'androidx.compose.material:material:1.0.0-beta05'
    // Material design icons
    implementation 'androidx.compose.material:material-icons-core:1.0.0-beta05'
    implementation 'androidx.compose.material:material-icons-extended:1.0.0-beta05'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.3.0-alpha07'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha04'
    // Integration with observables
    implementation 'androidx.compose.runtime:runtime-livedata:1.0.0-beta05'

    def room_version = "2.3.0"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    implementation 'com.google.dagger:hilt-android:2.35'
    kapt 'com.google.dagger:hilt-compiler:2.35'

    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:2.6.0-alpha02"
    implementation 'androidx.hilt:hilt-work:1.0.0-beta01'

    implementation 'androidx.preference:preference-ktx:1.1.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}
