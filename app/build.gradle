plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "dagger.hilt.android.plugin"
}

ext {
    publicRelease = project.hasProperty("public")
    isRelease = project.hasProperty("release")
}

@SuppressWarnings("GrMethodMayBeStatic")
def getVersionSuffix() {
    if (publicRelease) {
        return "-playstore"
    } else {
        return "-internal"
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "io.github.plastix.buzz"
        if(!isRelease) {
            applicationIdSuffix ".dev"
        }
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 9
        versionName "1.0-alpha05"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "boolean", "IS_PUBLIC_RELEASE", publicRelease.toString()
        versionNameSuffix getVersionSuffix()
    }

    buildFeatures {
        compose true
        buildConfig true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.2.0-beta01"
    }

    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}

dependencies {
    implementation(project(":core"))

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.appcompat:appcompat:1.4.0"
    implementation("com.squareup.okhttp3:okhttp:4.9.1")
    implementation("com.squareup.moshi:moshi:1.13.0")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.13.0")
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    implementation "androidx.compose.ui:ui:1.2.0-beta01"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:1.2.0-beta01"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:1.2.0-beta01"
    // Material Design
    implementation "androidx.compose.material:material:1.2.0-beta01"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core:1.2.0-beta01"
    implementation "androidx.compose.material:material-icons-extended:1.2.0-beta01"
    // Integration with activities
    implementation "androidx.activity:activity-compose:1.4.0"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1"
    // Integration with observables
    implementation "androidx.compose.runtime:runtime-livedata:1.2.0-beta01"

    def room_version = "2.4.0"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    implementation "com.google.dagger:hilt-android:2.40.1"
    kapt "com.google.dagger:hilt-compiler:2.40.1"

    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation "androidx.hilt:hilt-work:1.0.0"

    implementation "androidx.preference:preference-ktx:1.1.1"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"
}
